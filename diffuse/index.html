
<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8">
		<title> Diffuse (jgalazm) </title>		
		<script src="js/three.min.js"></script>
		<!--<script src="src/Three.js"></script>-->
		<script src="js/jquery-1.11.3.min.js"></script>				
		<script src="js/dat.gui.min.js"></script>				
		<script src="js/model.js"></script>
		<script>
		// 	var vshader = 'shaders/vshader.glsl';
		// 	var iFshader = 'shaders/initFShader.glsl';
		// 	var mFshader = 'shaders/modelFShader.glsl';
		// 	var sFshader = 'shaders/screenFShader.glsl';		
			$(function(){
				init();
			});
		</script>		

		<script type="x-shader/x-fragment" id="vshader">
			varying vec2 vUv;
			void main()
			{	
				vUv = uv;
				gl_Position = projectionMatrix* modelViewMatrix*vec4(position,1.0);
			}
		</script>
		<script type="x-shader/x-fragment" id="iFshader">		
			varying vec2 vUv;
			uniform sampler2D tSource;
			uniform vec2 delta;
			uniform vec4 colors[7];
			void main()
			{

			    float value = 0.0;//texture2D(tSource, vUv).r;

			    gl_FragColor = vec4((1.0-value)*colors[6].a, 0.0, 0.0, 1.0); 
			}
		</script>

		<script type="x-shader/x-fragment" id="mFshader">
			varying vec2 vUv;
			uniform sampler2D tSource;
			uniform vec2 delta;
			uniform vec2 texel;
			uniform vec2 mouse;
			uniform int mouseDown;
			uniform int boundaryCondition;
			uniform float heatSourceSign;
			uniform float heatIntensity;
			uniform float brushWidth;
			uniform int pause;
			void main()
			{
				//neighbors values
				float u_ij = texture2D(tSource, vUv).r;
				float u_imj = texture2D(tSource, vUv+vec2(-1.0*delta.x,0.0)).r;
				float u_ipj = texture2D(tSource, vUv+vec2(delta.x,0.0)).r;
				float u_ijm = texture2D(tSource, vUv+vec2(0.0,-1.0*delta.y)).r;
				float u_ijp = texture2D(tSource, vUv+vec2(0.0,delta.y)).r;
				float dt = 0.2*delta.x*delta.x;

				if (mouseDown==1){
					vec2 dist = (mouse-vUv)/texel;
					if (length(dist)<=brushWidth/2.0){
						u_ij += heatSourceSign*heatIntensity*dt;//*dt, but then it should also
						//have the diffusivity constant of the material in the equation.
						//anyway, everything is the same up to scaling by some space
						//and time constants, so it does not really matter.
					}
				}		
				if (pause == 0){
					//boundaries
					if (boundaryCondition == 0){
						if (vUv.x <=delta.x){
							gl_FragColor = vec4(0.0,0.0,0.0,1.0);
							return;
						}
						else if (vUv.x >=1.0-delta.x){
							gl_FragColor = vec4(0.0,0.0,0.0,1.0);
							return;
						}

						if (vUv.y <=delta.y){
							gl_FragColor = vec4(0.0,0.0,0.0,1.0);
							return;
						}
						else if (vUv.y>=1.0-delta.y) {
							gl_FragColor = vec4(0.0,0.0,0.0,1.0);
							return;
						}
					}
					else{
						if (vUv.x <=delta.x){
							gl_FragColor= vec4(u_ipj,0.0,0.0,1.0);
							return;
						}
						else if (vUv.x >=1.0-delta.x){
							gl_FragColor= vec4(u_imj,0.0,0.0,1.0);
							return;
						}

						if (vUv.y <=delta.y){
							gl_FragColor= vec4(u_ijp,0.0,0.0,1.0);
							return;
						}
						else if (vUv.y>=1.0-delta.y) {
							gl_FragColor= vec4(u_ijm,0.0,0.0,1.0);
							return;
						}	
					}
					//interior: u^{n+1}
					float u_np = u_ij;
					u_np += dt/(delta.x*delta.x)*(u_imj+u_ipj-2.0*u_ij);
					u_np += dt/(delta.x*delta.x)*(u_ijm+u_ijp-2.0*u_ij);

					gl_FragColor = vec4(u_np,0.0,0.0,1.0);
				}
				else{

					gl_FragColor = vec4(u_ij,0.0,0.0,1.0);
				}
			}
		</script>

		<script type="x-shader/x-fragment" id="sFshader">
			varying vec2 vUv;
			uniform sampler2D tSource;
			uniform vec2 delta;
			uniform vec4 colors[7];

			void main()
			{

			    float value = texture2D(tSource, vUv).r;
			    //int step = int(floor(value));
			    //float a = fract(value);
			    float t;
			    vec3 pseudoColor;
				// 
			    if(value <= colors[0].a){
			        pseudoColor = colors[0].rgb;
			    }
			    else if (value > colors[6].a){
			    	pseudoColor = colors[6].rgb;
			    }
			    else{
			    	for (int i=1; i<7; i++){
						vec4 cleft = colors[i-1];
						vec4 cright = colors[i];

						if (value>cleft.a && value <=cright.a){
							t = (value - cleft.a)/(cright.a - cleft.a);
							pseudoColor = mix(cleft.rgb, cright.rgb, t);
							break;
						}
					}			
				}

			    gl_FragColor = vec4(pseudoColor.r, pseudoColor.g,pseudoColor.b, 1.0); 
			}
		</script>			
	</head>		

	<body>
			
		<div id="simulation" style='display:inline-block;'>
			<canvas id="container" style="width:1024px;height:512px;border:1px solid #d0d0d0;" ></canvas>
		</div>
		<div id="controls" style='display:inline-block;'>
		</div>	
	</body>


</html>

